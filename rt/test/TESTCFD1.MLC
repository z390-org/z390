*********************************************************************
* Copyright 2006 Automated Software Tools Corporation               *
* Copyright 2013 Cat Herder Software, LLC                           *
* Copyright 2018 Joachim Bartz, Germany                             *
*                                                                   *
* This source code is part of z390 assembler/emulator package.      *
*                                                                   *
* The z390 package is distributed under GNU general public license  *
*                                                                   *
* Author - Don Higgins                                              *
*********************************************************************
* 2006-07-19 DSH         Initial version                            *
* 2006-11-10 DSH RPI-477 Add ASCII support to CTD and CFD,          *
*                        use IN=,OUPT=                              *
* 2006-11-29 DSH RPI-507 Support IN=REG in addition to IN=(REG ADDR)*
* 2006-12-02 DSH RPI-511 Support floating point format for INT128   *
*                        input                                      *
* 2006-12-03 DSH RPI-513 Add RC test                                *
* 2006-12-11 DSH RPI-514 Add DFP DD, ED, and LD tests               *
* 2008-01-14 DSH RPI-786 Use DFP COMP to handle diff canonizations  *
* 2008-04-22 DSH RPI-833 Add string quotes for HLASM compatibility  *
*********************************************************************

         PRINT DATA

TESTCTD1 RT1   MAIN
WORK     DC    XL16'00' INT128,E,D,L WORK AREA
DECIMAL  DC    CL45' '  DECIMAL WORK AREA
         EQUREGS
ABORT    ABEND 111
         MACRO
         TEST  &TYPE,&IN,&VER
         BALR  R10,0                             RPI-786
         USING *,R10                             RPI-786
         MVC   DECIMAL,&IN 
* TEST CFD OUT=RX
         CFD   CFD_&TYPE,OUT=WORK,IN=DECIMAL
         LTR   R15,R15
         BNZ   ABORT
         LX    R4,WORK
         TESTC &TYPE,&VER
* TEST CFD OUT=(REG)
         LA    R2,WORK 
         CFD   CFD_&TYPE,OUT=(R2),IN=DECIMAL
         LTR   R15,R15
         BNZ   ABORT
         LX    R4,WORK
         TESTC &TYPE,&VER
* TEST CFD OUT-REG (VALUE 0-15)
         CFD   CFD_&TYPE,OUT=R4,IN=DECIMAL
         LTR   R15,R15
         BNZ   ABORT
         TESTC &TYPE,&VER
         MEND
         MACRO
         TESTC &TYPE,&VAL
         AIF   ('&TYPE' EQ 'ED').ED_COMP
         AIF   ('&TYPE' EQ 'DD').DD_COMP
         AIF   ('&TYPE' EQ 'LD').LD_COMP
         AGO   .CLC_COMP
.ED_COMP ANOP
         LE    R0,&VAL
         LDETR R0,R0,0
         LDETR R4,R4,0
         CDTR  R0,R4
         AGO   .END_COMP
.DD_COMP ANOP
         LD    R0,&VAL
         CDTR  R0,R4
         AGO   .END_COMP
.LD_COMP ANOP
         LX    R0,&VAL
         CXTR  R0,R4
         AGO   .END_COMP
.CLC_COMP ANOP
         STX   R4,WORK
         LA    R1,L'&VAL
         BCTR  R1,0
         EX    R1,*+4+4
         B     *+4+6
         CLC   WORK(0),&VAL  VERIFY CTD CONSTANT WITH CFD RESULT
.END_COMP ANOP
         RT1   CCE
         MEND
         RT1   START
         WTO   'TEST CFD CONVERT FROM DISPLAY MACRO/SVC 171 SERVICE'
         TEST  INT128,=CL45'2',=FL16'2'
         TEST  INT128,=CL45'123E2',=FL16'12300'
         TEST  INT128,=CL45'123.5',=FL16'123'
         TEST  EH,=CL45'2',=EH'2'
         TEST  EB,=CL45'2',=EB'2'
         TEST  DH,=CL45'2',=DH'2'
         TEST  DB,=CL45'2',=DB'2'
         TEST  LH,=CL45'2',=LH'2'
         TEST  LB,=CL45'2',=LB'2'
         TEST  DD,=CL45'2',=DD'2'
         TEST  ED,=CL45'2',=ED'2'
         TEST  LD,=CL45'2',=LD'2'
         RT1   END
         EQUREGS REGS=FPR
         END