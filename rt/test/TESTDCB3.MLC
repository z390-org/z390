*********************************************************************
* Copyright 2005 Automated Software Tools Corporation               *
* Copyright 2013 Cat Herder Software, LLC                           *
* Copyright 2018 Joachim Bartz, Germany                             *
*                                                                   *
* This source code is part of z390 assembler/emulator package.      *
*                                                                   *
* The z390 package is distributed under GNU general public license  *
*                                                                   *
* Author - Don Higgins                                              *
*********************************************************************
* TESTDCB3.MLC                                                      *
* Copy RECFM=FB EBCDIC file to RECFM=FB EBCDIC file                 *
*********************************************************************
* 2005 09-30 DSH         Initial version                            *
*********************************************************************

TESTDCB3 SUBENTRY
         WTO   'TESTDCB3 COPY SYSUT1 FB FILE TO SYSUT2 FB'
         OPEN  (SYSUT1,(INPUT),SYSUT2,(OUTPUT),SYSOUT,(OUTPUT))
LOOP     EQU   *
         GET   SYSUT1,RECORD
         AP    PTOT,=P'1'
         MVC   DTOT,=X'40202020'
         ED    DTOT,PTOT
         PUT   SYSOUT,MSG
         PUT   SYSUT2,RECORD
         B     LOOP
EOF      CLOSE (SYSUT1,,SYSUT2,,SYSOUT)
         WTO   'TESTDCB3 ENDED OK'
         SUBEXIT
ABORT    EQU   *
         WTO   'TESTDCB3 ABORTING DUE TO I/O ERROR NOW'
         ABEND X'213',DUMP
SYSUT1   DCB   DDNAME=SYSUT1,EODAD=EOF,MACRF=GM,SYNAD=ABORT,           X
               RECFM=FB,LRECL=80,BLKSIZE=8000            
SYSUT2   DCB   DDNAME=SYSUT2,MACRF=PM,SYNAD=ABORT,                     X
               RECFM=FB,LRECL=80,BLKSIZE=8000
SYSOUT   DCB   DDNAME=SYSOUT,RECFM=FT,BLKSIZE=120,MACRF=PM
PTOT     DC    PL2'0'
MSG      DS    0CL120
         DC    C'REC#='
DTOT     DC    CL4' ',C' TEXT='
RECORD   DC    CL80' '
         DC    (MSG+120-*)C' '
         DCBD
         END