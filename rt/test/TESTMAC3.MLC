*********************************************************************
* Copyright 2005 Automated Software Tools Corporation               *
* Copyright 2013 Cat Herder Software, LLC                           *
* Copyright 2018 Joachim Bartz, Germany                             *
*                                                                   *
* This source code is part of z390 assembler/emulator package.      *
*                                                                   *
* The z390 package is distributed under GNU general public license  *
*                                                                   *
* Author - Don Higgins                                              *
*********************************************************************
* TESTMAC3.MLC                                                      *
* mz390 redefine instruction using inline macro, redefine global as *
* local, expand array, test tab vs space delimiter before comments  *
*********************************************************************
* 2005-09-30  DSH         Initial version                           *
* 2005-12-30  DSH RPI-49  Test redefine of instruction via inline   *
*                         macro                                     *
* 2006-01-22  DSH RPI-178 Test macro defining local overriding      *
*                         global                                    *
* 2006-01-22  DSH RPI-179 Test macro expanding arrays               *
*********************************************************************

TESTMAC3 SUBENTRY
         WTO   'TESTMAC3 - TEST REDEFINE INSTRUCTION VIA INLINE MACRO'
* TEST REDEFINE OF B AS BRC
         MACRO
&N       B     &P1
&N       BRC   15,&P1
         MEND
A        B     B
B        B     *+4
* TEST REDEFINE OF GLOBAL AS LOCAL FOR NEW MACRO
         MACRO
         DEFG1
         GBLC  &X
&X       SETC  'GLOBAL'
         MNOTE 'DEFG1 X=&X'
         MEND
         DEFG1
         MACRO
         DEFL1
         LCLC  &X
&X       SETC  'LOCAL'
         MNOTE 'DEFL1 X=&X'
         MEND
         DEFL1
         MACRO
         DEFG2
         GBLC  &X
         MNOTE 'DEFG2 X=&X'
         MEND
         DEFG2
* TEST MACRO ARRAY EXPANSION
         LCLA  &A(10),&B(10)
&A(1)    SETA  1
&A(10)   SETA  10
&A(15)   SETA  15
&B(1)    SETA  2
&B(10)   SETA  20
&B(15)   SETA  25
&B(25)   SETA  &B(35)  EXPAND TWICE IN ONE STATEMENT
         AIF   (&A(1)  NE 1).ERR
         AIF   (&A(10) NE 10).ERR
         AIF   (&A(15) NE 15).ERR
         AIF   (&B(1)  NE 2).ERR
         AIF   (&B(10) NE 20).ERR
         AIF   (&B(15) NE 25).ERR
         AIF   (&B(25) NE 0).ERR
         MNOTE 'A AND B EXPANSION OK'
         AGO   .OK
.ERR     MNOTE 12,'EXPANSION ERROR'
         MEXIT
.OK      ANOP

         WTO   'TESTMAC3 ENDED OK'
         SUBEXIT
         END